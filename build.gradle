// See also: https://github.com/quat1024/modern-forge-1.12-template

buildscript {
    repositories {
        mavenCentral()
        maven { url = "https://maven.minecraftforge.net/" }
    }

    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+"
    }
}

apply plugin: "java"
apply plugin: "net.minecraftforge.gradle"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
compileJava {
    sourceCompatibility = "8"
    targetCompatibility = "8"
}

repositories {
    maven {
        url = "https://maven.minecraftforge.net/"
    }

    // CurseMaven
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
    compileOnly "curse.maven:Hwyla-${cf_hwyla_id}:${cf_hwyla_api_id}"

    runtimeOnly fg.deobf("curse.maven:Hwyla-${cf_hwyla_id}:${cf_hwyla_full_id}")
}

minecraft {
    mappings channel: "stable", version: "39-1.12"

    runs {
        "client" {
            workingDirectory file("./run")
            mods { "${project.name}" { source sourceSets.main } }
        }

        "server" {
            workingDirectory file("./run/server")
            mods { "${project.name}" { source sourceSets.main } }
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", mc_version

    filesMatching("mcmod.info") {
        expand "version": project.version, "mcversion": mc_version
    }
}

//When Forge 1.12 loads mods from a directory that's been put on the classpath, it expects to find resources in the same directory.
//Default Gradle behavior puts resources in ./build/resources/main instead of ./build/classes/main/java. Let's change that.
sourceSets.all { it.output.resourcesDir = it.output.classesDirs.getFiles().iterator().next() }